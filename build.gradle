buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

apply plugin: 'com.bmuschko.nexus'

sourceCompatibility = JavaVersion.VERSION_11
group = "io.github.oemergenc"
version = "0.0.3"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation group: 'com.google.cloud.bigtable', name: 'bigtable-hbase-2.x-shaded', version: '1.4.0'
    implementation(group: 'com.flipkart', name: 'hbase-object-mapper', version: '1.13') {
        exclude group: 'org.apache.hbase', module: 'hbase-client'
    }

    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.6'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testImplementation "org.testcontainers:spock:1.12.5"
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Add required metadata to POM
            pom.project {
                name = 'hbase orm extension'
                description = 'some extension for the hbase orm library'
                url = 'https://github.com/oemergenc/hbase-orm-extensions'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'oemergenc'
                        name = 'Oemer Genc'
                        email = 'oemergenc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:oemergenc/hbase-orm-extensions.git'
                    developerConnection = 'scm:git@github.com:oemergenc/hbase-orm-extensions.git'
                    url = 'https://github.com/oemergenc/hbase-orm-extensions'
                }
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = false
}

signing {
    useGpgCmd()
    sign configurations.archives
}

nexusStaging {
    packageGroup = "io.github.oemergenc"
}
