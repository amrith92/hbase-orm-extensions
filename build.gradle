buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'java-library'
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

tasks.register("javadocJar", Jar) {
    from javadoc
    classifier "javadoc"
}

sourceCompatibility = JavaVersion.VERSION_11
group = "io.github.oemergenc"

repositories {
    mavenCentral()
}
ext {
    bintrayUser = project.findProperty('bintrayUser')
    bintrayApiKey = project.findProperty('bintrayApiKey')
    nexusUser = project.findProperty('nexusUser')
    nexusPassword = project.findProperty('nexusPassword')
}
dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    compileOnly 'org.projectlombok:lombok:1.18.10'
    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation group: 'com.google.cloud.bigtable', name: 'bigtable-hbase-2.x-shaded', version: '1.4.0'
    implementation(group: 'com.flipkart', name: 'hbase-object-mapper', version: '1.13') {
        exclude group: 'org.apache.hbase', module: 'hbase-client'
    }
    implementation group: 'net.vidageek', name: 'mirror', version: '1.6.1'

    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.6'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testImplementation "org.testcontainers:spock:1.12.5"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'hbase orm extension'
                description = 'some extension for the hbase orm library'
                url = 'https://github.com/oemergenc/hbase-orm-extensions'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'oemergenc'
                        name = 'Oemer Genc'
                        email = 'oemergenc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:oemergenc/hbase-orm-extensions.git'
                    developerConnection = 'scm:git@github.com:oemergenc/hbase-orm-extensions.git'
                    url = 'https://github.com/oemergenc/hbase-orm-extensions'
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['mavenJava']
    pkg {
        repo = 'hbase-orm-extensions'
        name = 'hbase-orm-extensions'
        vcsUrl = 'https://github.com/oemergenc/hbase-orm-extensions.git'
        licenses = ["Apache-2.0"]
        version {
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                user = nexusUser
                password = nexusPassword
            }
        }
    }
}
